#include "sai/c2/pal.h"
#include "sai/target.h"
#include "sai/memmap.h"
#include "sai/md/vdp.h"
#include "sai/c2/prot.h"

	.section	.text

	.global	sai_pal_init
sai_pal_init:
	calla6_safe sai_min_pal_init
	rts

	.global	sai_min_pal_init
sai_min_pal_init:
	move.w	#VDP_MODESET3_DEFAULT & ~(VDP_MODESET3_ADMUX), VDP_BASE+VDP_OFFS_CTRL

	sf	SYSC_PROT_OFFS_PALBANK
	calla1 pal_init_sub
	move.b	#1, SYSC_PROT_OFFS_PALBANK
	calla1 pal_init_sub
	move.b	#2, SYSC_PROT_OFFS_PALBANK
	calla1 pal_init_sub
	move.b	#3, SYSC_PROT_OFFS_PALBANK
	calla1 pal_init_sub

	move.w	#VDP_MODESET3_DEFAULT, VDP_BASE+VDP_OFFS_CTRL

	clr.w	g_sai_pal_cmd_count
	
	jmp	(a6)

; clears 1K of CRAM.
; a1 = return
pal_init_sub:
	lea	CRAM_BASE, a0
	moveq	#0, d0
	move.w	#($400/4)-1, d1
0:
	move.l	d0, (a0)+
	dbf	d1, 0b
	jmp	(a1)
