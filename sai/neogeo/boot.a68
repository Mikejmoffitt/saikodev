;
; Saikodev Neo-Geo entry points.
;
; The four entry points must be provided by the user.
;
#include "sai/input.h"
#include "sai/macro.h"
#include "sai/memmap.h"
#include "sai/target.h"
#include "sai/neogeo/reg.h"
#include "sai/neogeo/sysrom.h"

	.extern	sai_crt0_begin

	.extern	sai_neo_bram_data
	.extern	sai_neo_bram_bytes

	.section	.text

	.global	start
	.global	_start

	.extern	sai_neo_entry_user
	.extern	sai_neo_entry_player_start

	.global	start
start = SAI_NEO_SYSTEM_RESET

	.global	startup_err

_start:
start:
	move.w	#$2700, sr
	lea	__stack, sp

	.global sai_neo_entry_user_internal
sai_neo_entry_user_internal:
	move.b	g_sai_neo_user_request, d1
;
; User Command 0 - Init
;
user_cmd0:
	; Clear BRAM data if in first init for the game.
	cmpi.b	#SAI_NEO_UREQ_INIT, d1
	bne.s	user_cmd1
	move.b	#SAI_NEO_USERMODE_INIT, g_sai_neo_user_mode_for_system
	lea	sai_neo_bram_data, a0
	moveq	#sai_neo_bram_bytes-1, d0
1:
	sf	SAI_NEO_REG_DIPSW  ; Watchdog
	sf	(a0)+
	dbf	d0, 1b
	bra.s	user_begin
;
; User Command 1 - Eyecatch
;
user_cmd1:
	cmpi.b	#SAI_NEO_UREQ_EYE_CATCH, d1
	beq.s	user_begin  ; Let the program handle it.
;
; User Command 2 - Attract / Demonstration
;
user_cmd2:
	cmpi.b	#SAI_NEO_UREQ_ATTRACT, d1
	bne.s	user_cmd3
	move.b	#SAI_NEO_USERMODE_ATTRACT, g_sai_neo_user_mode_for_system
	bra.s	user_begin
;
; User Command 3 - Title / Game
;
user_cmd3:
	cmpi.b	#SAI_NEO_UREQ_TITLE, d1
	bne.s	user_ng
	move.b	#$01, $10FEC5  ; TODO: real symbol name
user_begin:
	jmp	(sai_crt0_begin).l
;
; Unknown / Invalid command - Just return.
;
user_ng:
	jmp	SAI_NEO_SYSTEM_RETURN


	.global	sai_neo_entry_player_start_internal
sai_neo_entry_player_start_internal:
	jmp	(sai_neo_entry_player_start).l

	.global sai_neo_entry_coin_sound_internal
sai_neo_entry_coin_sound_internal:
	; TODO: send pre-determined sound ID to Z80.
	.global	sai_neo_entry_demo_end_internal
sai_neo_entry_demo_end_internal:
	rts


softreset:
	jmp	sai_crt0_begin

startup_err:
	jmp	start

sai_neo_init_bram_sub:

	rts
