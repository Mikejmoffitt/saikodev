	.global	sai_crt0_begin
	.extern	main

#include	"sai/memmap.h"

sai_crt0_begin:
	; Clear bss
	lea	(__bss_start).l, a0
	move.w	#__bss_size, d7
	beq.s	no_bss_clear
	lsr.w	#1, d7
	subq.w	#1, d7
	moveq	#0, d0
0:
	move.w	d0, (a0)+
	dbra	d7, 0b
no_bss_clear:

	; copy data to work RAM
	lea	(__data_src).l, a0  ; Data follows text section
	lea	WRAM_BASE, a1  ; .data is at start of WRAM
	move.w	#__data_size, d0
	beq.s	no_data_copy
	lsr.w	#1, d0
	subq.w	#1, d0

0:
	move.w	(a0)+, (a1)+
	dbra	d0, 0b

no_data_copy:
	lea	__stack, sp
	moveq	#0, d0
	move.l	d0, d1
	move.l	d0, d2
	move.l	d0, d3
	move.l	d0, d4
	move.l	d0, d5
	move.l	d0, d6
	move.l	d0, d7
	move.l	d0, a0
	move.l	d0, a1
	move.l	d0, a2
	move.l	d0, a3
	move.l	d0, a4
	move.l	d0, a5
	move.l	d0, a6
	andi.w	#$F8FF, sr  ; Enable interrupts
	jmp	(main)
