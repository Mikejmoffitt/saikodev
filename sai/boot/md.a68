; saikodev megadrive and C2 startup
#include "sai/macro.h"
#include "sai/memmap.h"
#include "sai/target.h"
#include "sai/io/md.h"

;
; Vector table
;
	.section	.text.keepboot

	.global	start

	.extern	_etext
	.extern	_stext
	.extern	_edata
	.extern	_sdata
	.extern	main

	.extern	md_vdp_min_init

	.global	_v_table
	.global	_start
	.global	startup_forever

	.extern	sai_crt0_begin

#include "sai/video/vdp.h"

#if SAI_TARGET == SAI_TARGET_C2
#include "sai/io/c2.h"
#else
#include "sai/io/md.h"
#endif  // SAI_TARGET

	; TODO: Header
;
; Entry point
;

; Routines here do not use a call stack, but instead store the return address in
; the a6 register; until the WRAM test has completed it is not assumed that the
; RAM actually works.
	.org	$200
_start:
start:
	move.w	#$2700, sr
	lea	__stack, sp

#if SAI_TARGET == SAI_TARGET_MD
	calla6	sai_min_tmss_init
	calla6	sai_min_md_z80_init
	calla6	sai_min_md_pad_init
#elif SAI_TARGET==SAI_TARGET_C2
	calla6	sai_c2_min_init
#endif  // MDK_TARGET_C2

	calla6	sai_min_psg_init
	calla6	sai_min_vdp_init

softreset:
	jmp	sai_crt0_begin

startup_forever:
#if SAI_TARGET == SAI_TARGET_C2
	; TODO: Start button to recovery from error or something
	bra.w	start
#else
	move.l	#$00400040, (MD_IO_CTRL1-1).l  ; Set TH pin as an output
	move.b	#$00, (MD_IO_DATA1).l  ; TH low
	btst	#5, (MD_IO_DATA1).l  ; Check start button
	bne.s	startup_forever
startup_forever_wait_start_release:
	btst	#5, (MD_IO_DATA1).l  ; Check start button
	beq.s	startup_forever_wait_start_release
#endif
	bra.w	start


;
; Init and test Subroutines
;

; d0 = test word
; a6 = return
startup_wram_test_sub:
	lea	WRAM_BASE, a4
	move.w	#(WRAM_SIZE/2) - 1, d7
wram_test_loop:
	move.w	d0, (a4)
	cmp.w	(a4)+, d0
	bne.s	wram_test_failed
	dbra	d7, wram_test_loop
	jmp	(a6)

wram_test_failed:
	; knowingly stomping on the a6 return pointer.
	; TODO
;	calla6	md_error_startup_wram_ng_display
	bra.w	startup_forever

#if SAI_TARGET == SAI_TARGET_MD
	.global	sai_min_tmss_init
; a6.l = return
sai_min_tmss_init:
	; TMSS
	move.b	MD_IO_VERSION, d0
	andi.b	#$0F, d0
	beq.s	0f
	move.l	#$53454741, TMSS_PORT  ; "SEGA"
0:
	jmp	(a6)
#endif


;
